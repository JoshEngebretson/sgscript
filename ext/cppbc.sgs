
include "string", "re", "fmt", "io", "os";


global BRACE_START_MAP = { ">" = "<", "]" = "[", "}" = "{", ")" = "(" };

global ERR_OOC = "SGS_OBJECT outside class/struct";


global DEFAULT_OBJ_IFUNCS =
{
	"destruct" = "_sgsimpl_destruct",
	"gcmark" = "_sgsimpl_gcmark",
	"getindex" = "_sgsimpl_getindex",
	"setindex" = "_sgsimpl_setindex",
	"dump" = "_sgsimpl_dump",
};

global OBJ_IFUNC_POS =
{
	"destruct"  = 0,
	"gcmark"    = 1,
	"getindex"  = 2,
	"setindex"  = 3,
	"convert"   = 4,
	"serialize" = 5,
	"dump"      = 6,
	"getnext"   = 7,
	"call"      = 8,
	"expr"      = 9,
};

global OBJ_IFUNC_COUNT = 10;


function string_find_rev_any( str, what, pos )
{
	if( pos === null )
		pos = str.length;
	
	min = 0;
	foreach( item : what )
	{
		npos = string_find_rev( str, item, pos );
		if( npos !== null && npos > min )
			min = npos;
	}
	
	return if( min <= pos, min, null );
}

function regex_find_any( str, what, pos )
{
	out = re_match( str, what, RE_RETURN_BOTH, pos ?? 0 );
	if( out !== false )
		return out[0][1], out[0][0];
}


function code_to_linenum( code )
{
	return string_count( string_replace( code, ["\r\n","\r"], "\n" ), "\n" );
}
function code_to_lines( code )
{
	return string_repeat( "\n", code_to_linenum( code ) );
}


global CPPBC_LINENUMBERS = [];
function cppbc_setline_codepos( code, pos, pushrel )
{
	global CPPBC_LINENUMBERS;
	code = string_part( code, 0, pos );
	ln = code_to_linenum( code );
	if( !pushrel )
		CPPBC_LINENUMBERS.clear();
	else
		ln += CPPBC_LINENUMBERS.last + pushrel;
	CPPBC_LINENUMBERS.push( ln );
}
function cppbc_setline_pop(){ CPPBC_LINENUMBERS.pop(); }
function cppbc_getline()
{
	if( !CPPBC_LINENUMBERS ) return "<unknown line>";
	return CPPBC_LINENUMBERS.last;
}


function cppbc_error( error ){ errprintln( " line ", cppbc_getline(), ": ERROR: ", error ); app_exit(1); }
function cppbc_error_ext( error, code, pos ){ cppbc_setline_codepos( code, pos ); cppbc_error( error ); }


function cppbc_preprocess( code )
{
	if( typeid(code) != VT_STRING )
		cppbc_error( "cppbc_preprocess: expected string" );
	
	pos = 0;
	out = "";
	
	for(;;)
	{
		(to,which) = regex_find_any( code, """#/\*|//|'|"#""", pos );
		if( to === null )
		{
			out $= string_part( code, pos );
			break;
		}
		
		out $= string_part( code, pos, to - pos );
		
		if( which == "/*" )
		{
			out $= " ";
			pos = string_find( code, "*/", to );
			if( pos === null )
				cppbc_error( "error in code: unfinished comment detected" );
			out $= code_to_lines( string_part( code, to, pos - to ) );
			pos += 2;
		}
		else if( which == "//" )
		{
			out $= " ";
			npos = regex_find_any( code, "#\r\n|\r|\n#", to );
			if( npos === null )
				break;
			pos = npos;
		}
		else if( which == "'" || which == "\"" )
		{
			regex = "#\\\\|" $ which $ "#";
			cpos = to;
			for(;;)
			{
				npos = re_match( code, regex, RE_RETURN_BOTH, cpos );
				if( npos === false )
					cppbc_error( "error in code: unfinished string literal detected" );
				cpos = npos[0][2];
				if( npos[0][0] == "\\" )
					cpos++;
				else
					break;
			}
			out $= which;
			pos = cpos;
		}
	}
	
	return out;
}


function cppbc_remove_blocks( code )
{
	pos = 0;
	out = "";
	level = 0;
	begin = null;
	
	for(;;)
	{
		match = re_match( code, "#\\{|\\}#", RE_RETURN_BOTH, pos );
		if( match === false )
		{
			out $= string_part( code, pos );
			break;
		}
		
		which = match[0][0];
		from = match[0][1];
		to = match[0][2];
		
		if( level == 0 )
			out $= string_cut( code, pos, from - 1 );
		
		if( which == "{" )
		{
			if( level == 0 )
				begin = from;
			level++;
		}
		else // }
		{
			level--;
			if( level == 0 )
			{
				out $= ";";
				out $= code_to_lines( string_part( code, begin, from - begin ) );
				begin = null;
			}
			else if( level < 0 )
				cppbc_error( "error in code: curved bracket mismatch" );
		}
		pos = to;
	}
	
	if( level > 0 )
		cppbc_error( "error in code: curved bracket mismatch" );
	
	return out;
}


function cppbc_type_is_voidptr( type )
{
	return string_replace( type, [" ","\t","\n","\r"], "" ) == "void*";
}


function cppbc_fix_type( type )
{
	type = string_trim( type );
	type = string_trim( re_replace( type, "#^const([ \t\n\r])#", "$1" ) );
	type = string_trim( re_replace( type, "#&$#", "" ) );
	type = string_trim( re_replace( type, "#([ \t\n\r])const$#", "$1" ) );
	
	if( !type )
		return cppbc_error( "missing type detected" );
	
	if( type.length && type.last == ">"c )
		type $= " ";
	
	return type;
}


/* Find boundaries of block {...} "pos" is in */
function cppbc_block_bounds( code, pos )
{
	upper = null;
	lower = null;
	level = 0;
	
	// LOWER bounds
	for(;;)
	{
		pa = string_find_rev( code, "{", pos );
		pb = string_find_rev( code, "}", pos );
		if( pa === null )
			break;
		if( pb !== null && pb > pa )
		{
			pos = pb - 1;
			level++;
			continue;
		}
		else
		{
			pos = pa - 1;
			level--;
			if( level >= 0 )
				continue;
			lower = pa;
			break;
		}
	}
	
	if( lower === null )
		cppbc_error_ext( ERR_OOC, code, pos );
	
	// UPPER bounds
	for(;;)
	{
		pa = string_find( code, "}", pos );
		pb = string_find( code, "{", pos );
		if( pa === null )
			break;
		if( pb !== null && pb < pa )
		{
			pos = pb + 1;
			level++;
			continue;
		}
		else
		{
			pos = pa + 1;
			level--;
			if( level >= 0 )
				continue;
			upper = pa;
			break;
		}
	}
	
	if( upper === null )
		cppbc_error_ext( ERR_OOC, code, pos );
	
	return [ lower, upper ];
}


function cppbc_get_vdchar_type( char )
{
	if( fmt_charcc( char, "a-zA-Z0-9_." ) )
		return "N";
	if( fmt_charcc( char, " \t\v\r\n" ) )
		return "S";
	return char;
}


function cppbc_find_fp_name( scode )
{
	if( scode.last != ")" )
		cppbc_error( "unrecognized sequence: \"" $ string_implode( scode, "" ) $ "\"" );
	
	level = 0;
	pos = scode.size - 1;
	skipped = false;
	while( pos > 0 )
	{
		item = scode[ pos ];
		if( item == ")" )
		{
			level++;
		}
		else if( item == "(" )
		{
			level--;
			if( level == 0 )
				skipped = true;
		}
		else if( re_match( item, "#^[a-zA-Z0-9_]+$#" ) && skipped )
		{
			return pos;
		}
		pos--;
	}
	
	return null;
}


function cppbc_parse_vardecl( code, bSkipName )
{
	out = [];
	
	code = string_trim( code );
	if( string_find( code, "SGS_ALIAS" ) === 0 )
	{
		inbr = string_find( code, "(" );
		outbr = string_find_rev( code, ")" );
		code = string_part( code, inbr + 1, outbr - inbr - 1 );
		code = string_trim( code );
	}
	
	// remove assignment
	code = re_replace( code, "#[ \n\r\t]*?=.*$#", "" );
	
	// remove bitfield size
	code = re_replace( code, "#[ \n\r\t]*:[ \n\r\t]*[0-9]+$#", "" );
	
	lct = null;
	for( i = 0; i < code.length; ++i )
	{
		cc = chr( code[i] );
		cct = cppbc_get_vdchar_type( cc );
		if( cct !== lct || ( cct != "N" && cct != "S" ) )
			out.push("");
		lct = cct;
		out[out.size-1] $= cc;
	}
	
	name = "";
	if( lct != ")" )
	{
		// basic typedef - identifier at end
		if( out.size <= 0 )
			cppbc_error( "type not specified" );
		if( !bSkipName || out.size != 1 )
			name = string_trim( out.pop() );
		type = string_trim( string_implode( out, "" ) );
	}
	else
	{
		// function pointer - identifier in right side of 2nd level-0 brackets from the end
		pos = cppbc_find_fp_name( out );
		if( pos === null && !bSkipName )
			cppbc_error( "could not parse type: \"" $ string_implode( out, "" ) $ "\"" );
		if( pos !== null )
		{
			name = out[ pos ];
			out.erase( pos );
		}
		type = string_implode( out, "" );
	}
	
	return {
		name = name,
		type = type,
	};
}


function cppbc_split_cs( code )
{
	code = string_trim( code );
	if( code == "" )
		return [];
	
	out = [""];
	stack = [];
	
	for( i = 0; i < code.length; ++i )
	{
		cc = chr( code[i] );
		if( cc == "<" || cc == "(" || cc == "[" )
		{
			stack.push( cc );
		}
		else if( cc == ">" || cc == ")" || cc == "]" )
		{
			if( @BRACE_START_MAP[ cc ] )
				tcc = BRACE_START_MAP[ cc ];
			if( !stack.size || stack.last != tcc )
				cppbc_error( "brace mismatch in argument list, stack: "$stack );
			stack.pop();
		}
		else if( cc == "," && stack.size == 0 )
		{
			out.push( "" );
			continue;
		}
		
		out[out.size-1] $= cc;
	}
	
	return out;
}


function cppbc_examine_method( code, is_static, methodname )
{
	code = string_trim( code );
	if( string_find( code, "SGS_ALIAS" ) === 0 )
	{
		inbr = string_find( code, "(" );
		outbr = string_find_rev( code, ")" );
		code = string_part( code, inbr + 1, outbr - inbr - 1 );
		code = string_trim( code );
	}
	
	// remove "virtual"
	code = re_replace( code, "#^virtual[ \t\v\r\n]+#", "" );
	
	ispmatch = re_match( code, "#([a-zA-Z0-9_]+)[ \t\v\r\n]*\\(#", RE_RETURN_BOTH );
	
	if( ispmatch === false )
		cppbc_error( "could not parse method" );
	
	name = ispmatch[1][0];
	rvd = cppbc_parse_vardecl( string_part( code, 0, ispmatch[0][1] ), true );
	
	ptypes = [];
	
	// remove "const"
	code = re_replace( code, "#\\)[^\\)]*$#", ")" );
	
	params = cppbc_split_cs( string_cut( code, ispmatch[0][2], -2 ) );
	foreach( param : params )
	{
		pvd = cppbc_parse_vardecl( param, true );
		ptypes.push( pvd.type );
	}
	
	return {
		what = "METHOD",
		name,
		is_static,
		varname = methodname || name,
		type = rvd.type,
		params = ptypes,
	};
}

function cppbc_examine_method_named( code, is_static )
{
	sp = fmt_string_parser( code );
	
	sp.skipcc( "( \t\v\r\n" );
	name = sp.readcc( "a-zA-Z0-9_." );
	sp.skipcc( ") \t\v\r\n" );
	
	return cppbc_examine_method( sp.read( 65536 ), is_static, name );
}


function cppbc_examine_property( code )
{
	access = 0;
	
	sp = fmt_string_parser( code );
	
	sp.skipcc( " \t\v\r\n" );
	item = sp.readcc( "a-zA-Z0-9_" );
	begoff = sp.stream_offset;
	
	if( item == "READ" )
		access = -1;
	else if( item == "WRITE" )
		access = 1;
	else
		begoff = 0;
	
	vd = cppbc_parse_vardecl( string_part( code, begoff ) );
	
	return {
		what = "PROPERTY",
		name = vd.name,
		type = vd.type,
		access = access,
	};
}


function cppbc_examine_property_func( code )
{
	access = 0;
	
	sp = fmt_string_parser( code );
	
	sp.skipcc( " \t\v\r\n" );
	items = [];
	item = "";
	bracestack = 0;
	if( sp.getchar() != "(" )
	{
		cppbc_error( "PROPERTY_FUNC: '(' expected" );
	}
	while( sp.at_end == false )
	{
		prevoff = sp.stream_offset;
		sp.skipcc( " \t\v\r\n" );
		curoff = sp.stream_offset;
		ch = sp.getchar();
		at_end = bracestack == 0 && ch == ")";
		if( ( curoff > prevoff || at_end ) && item.length )
		{
			items.push( item );
			item = "";
		}
		if( at_end )
			break;
		if( ch == "(" )
			bracestack++;
		else if( ch == ")" )
			bracestack--;
		item $= ch;
	}
	if( sp.at_end )
	{
		cppbc_error( "PROPERTY_FUNC: unexpected end of file before closing brace" );
	}
	
	readmethod = null;
	writemethod = null;
	readcallback = null;
	writecallback = null;
	varname = null;
	validate = null;
	source = null;
	lastitem = "";
	
	function specitem( item )
	{
		return item == "READ" || item == "WRITE" ||
			item == "READ_CALLBACK" || item == "WRITE_CALLBACK" ||
			item == "VARNAME" || item == "VALIDATE" || item == "SOURCE";
	}
	
	for( i = 0; i < items.size; i = iend )
	{
		istart = i + 1;
		for( iend = istart; iend < items.size; ++iend )
			if( specitem( items[ iend ] ) )
				break;
		arg = null;
		if( iend > istart )
		{
			arg = string_implode( items.part( istart, iend - istart ), " " );
		}
		
		item = items[ i ];
		if( item == "READ" )
		{
			if( arg )
				readmethod = arg;
			else
				readmethod = true;
		}
		else if( item == "WRITE" )
		{
			if( arg )
				writemethod = arg;
			else
				writemethod = true;
		}
		else if( item == "READ_CALLBACK" )
		{
			if( arg )
				readcallback = arg;
			else
				cppbc_error( "READ_CALLBACK: expected method name" );
		}
		else if( item == "WRITE_CALLBACK" )
		{
			if( arg )
				writecallback = arg;
			else
				cppbc_error( "WRITE_CALLBACK: expected method name" );
		}
		else if( item == "VARNAME" )
		{
			if( arg )
				varname = arg;
			else
				cppbc_error( "VARNAME: expected variable name" );
		}
		else if( item == "VALIDATE" )
		{
			if( arg )
				validate = arg;
			else
				cppbc_error( "VALIDATE: expected data source" );
		}
		else if( item == "SOURCE" )
		{
			if( arg )
				source = arg;
			else
				cppbc_error( "SOURCE: expected data source" );
		}
		else
			cppbc_error( "unexpected item: " $ item );
	}
	
	vd = cppbc_parse_vardecl( string_part( code, sp.stream_offset ) );
	
	return {
		what = "PROPERTY_FUNC",
		name = varname || vd.name,
		type = vd.type,
		read = readmethod,
		write = writemethod,
		readcb = readcallback,
		writecb = writecallback,
		varname = varname,
		srcname = if( source, source, vd.name ),
		validate = validate,
	};
}


function cppbc_examine_ifunc( code )
{
	ispmatch = re_match( code, "#([a-zA-Z0-9_]+)[ \t\v\r\n]*\\(#", RE_RETURN_BOTH );
	if( ispmatch === false )
		cppbc_error( "could not parse method (ifunc)" );
	
	typematch = re_match( code, "#([a-zA-Z0-9_]+)[ \t\v\r\n)]+#", RE_RETURN_BOTH );
	if( typematch === false )
		cppbc_error( "could not find interface function type" );
	
	name = ispmatch[1][0];
	type = typematch[1][0];
	
	type = string_tolower( type );
	if( !isset( OBJ_IFUNC_POS, type ) )
		cppbc_error( "unrecognized interface function type: " $ type );
	
	return {
		what = "IFUNC",
		name = name,
		type = type,
	};
}


function cppbc_examine_namelist( code, type )
{
	sp = fmt_string_parser( code );
	
	sp.skipcc( "( \t\v\r\n" );
	items = [];
	while( ( item = sp.readcc( "a-zA-Z0-9_." ) ) !== "" )
	{
		items.push( item );
		sp.skipcc( " \t\v\r\n" );
	}
	sp.skipcc( ")" );
	
	out = [];
	foreach( item : items )
		out.push({ what = type, name = item });
	return out;
}

function cppbc_examine_backingstore( code )
{
	code = string_trim( code );
	code = string_cut( code, 1, -2 );
	code = string_trim( code );
	read = true;
	write = true;
	if( re_match( code, "#^READ#" ) )
	{
		code = string_part( code, 4 );
		write = false;
	}
	else if( re_match( code, "#^WRITE#" ) )
	{
		code = string_part( code, 5 );
		read = false;
	}
	return { code, read, write };
}


function cppbc_examine_object( code, pos )
{
	// get class/struct bounds
	bounds = cppbc_block_bounds( code, pos );
	cppbc_setline_codepos( code, bounds[0] );
	
	// get class/struct name
	class_pos = bounds[0] + 1;
	for(;;)
	{
		class_pos = string_find_rev_any( code, ["class","struct"], class_pos - 1 );
		if( class_pos === null )
			cppbc_error_ext( ERR_OOC, code, pos );
		if( re_match( string_part( code, class_pos - 1, 8 ), "#(^|[ \t\v\n\r])(class|struct)[ \t\v\n\r]#" ) )
			break;
	}
	
	tp = fmt_string_parser( code, class_pos );
	tp.skipcc( "a-z" );
	tp.skipcc( " \t\v\n\r" );
	classname = tp.readcc( "a-zA-Z0-9_" );
	println( "Detected '" $ classname $ "'" );
	
	// gather all properties/methods
	blockcode = string_cut( code, bounds[0] + 1, bounds[1] - 1 );
	blockcode = cppbc_remove_blocks( blockcode );
	
	bstores = [];
	inherited = {};
	inh_matches = re_match_all( blockcode, "#SGS_OBJECT_INHERIT[ \t\v\n\r]*\\(([a-zA-Z0-9_ \t\v\n\r,]*)\\)#ms", RE_RETURN_CAPTURED );
	foreach( match : inh_matches )
	{
		inhlist = match[1];
		inhlist = string_replace( inhlist, [" ","\t","\v","\n","\r"], "" );
		foreach( inhitem : string_explode( inhlist, "," ) )
			inherited[ inhitem ] = true;
	}
	inherited = get_keys( inherited );
	if( inherited.size > 1 )
		cppbc_error( "multiple inheritance is not supported" );
	
	items = [];
	matches = re_match_all( blockcode, "#("
		$"SGS_METHOD_NAMED|SGS_METHOD|"
		$"SGS_STATICMETHOD_NAMED|SGS_STATICMETHOD|"
		$"SGS_PROPERTY_FUNC|SGS_PROPERTY|"
		$"SGS_IFUNC|SGS_GCREF|"
		$"SGS_DUMP|SGS_NODUMP|SGS_BACKING_STORE)"
		$"(.*?)[;\\{]#ms", RE_RETURN_BOTH );
	foreach( match : matches )
	{
		cppbc_setline_codepos( blockcode, match[0][1], 1 );
		item = null;
		if( match[1][0] == "SGS_METHOD" )
			item = cppbc_examine_method( match[2][0], false );
		else if( match[1][0] == "SGS_STATICMETHOD" )
			item = cppbc_examine_method( match[2][0], true );
		else if( match[1][0] == "SGS_METHOD_NAMED" )
			item = cppbc_examine_method_named( match[2][0], false );
		else if( match[1][0] == "SGS_STATICMETHOD_NAMED" )
			item = cppbc_examine_method_named( match[2][0], true );
		else if( match[1][0] == "SGS_PROPERTY" )
			item = cppbc_examine_property( match[2][0] );
		else if( match[1][0] == "SGS_PROPERTY_FUNC" )
			item = cppbc_examine_property_func( match[2][0] );
		else if( match[1][0] == "SGS_IFUNC" )
			item = cppbc_examine_ifunc( match[2][0] );
		else if( match[1][0] == "SGS_GCREF" )
			item = cppbc_examine_namelist( match[2][0], "GCREF" );
		else if( match[1][0] == "SGS_DUMP" )
			item = cppbc_examine_namelist( match[2][0], "DUMP" );
		else if( match[1][0] == "SGS_NODUMP" )
			item = cppbc_examine_namelist( match[2][0], "NODUMP" );
		else if( match[1][0] == "SGS_BACKING_STORE" )
			bstores.push( cppbc_examine_backingstore( match[2][0] ) );
		
		if( typeof(item) == "array" )
		{
			foreach( ii : item )
				items.push( ii );
		}
		else if( item !== null )
			items.push( item );
		
		cppbc_setline_pop();
	}
	
	return
	{
		name = classname,
		is_lite = re_match( blockcode, "#SGS_OBJECT_LITE#" ),
		new_iface = ( NEW_IFACE
			+ re_match( blockcode, "#SGS_NEW_IFACE#" )
			- re_match( blockcode, "#SGS_OLD_IFACE#" ) ) > 0,
		items,
		no_export = re_match( blockcode, "#SGS_NO_EXPORT#" ),
		no_destruct = re_match( blockcode, "#SGS_NO_DESTRUCT#" ),
		inherited,
		bstores,
	};
}


function cppbc_find_objects( code )
{
	inh_starts = [];
	inh_ends = [];
	matches = re_match_all( code, "#SGS_CPPBC_INHERIT_BEGIN#", RE_RETURN_OFFSETS );
	foreach( match : matches )
		inh_starts.push( match[0][0] );
	matches = re_match_all( code, "#SGS_CPPBC_INHERIT_END#", RE_RETURN_OFFSETS );
	foreach( match : matches )
		inh_ends.push( match[0][0] );
	
	matches = re_match_all( code, "#SGS_OBJECT#", RE_RETURN_OFFSETS );
	objects = [];
	foreach( match : matches )
	{
		pos = match[0][0];
		object = cppbc_examine_object( code, pos );
		objects.push( object );
		
		// check if object is inherited (cannot export those)
		inhnum = 0;
		foreach( at : inh_starts )
		{
			if( at > pos )
				break;
			else
				inhnum++;
		}
		foreach( at : inh_ends )
		{
			if( at > pos )
				break;
			else
				inhnum--;
		}
		if( inhnum > 0 )
			object.no_export = true;
	}
	
//	printvar_ext( objects, 10 );
	return objects;
}


function cppbc_generate_code( objects, includes )
{
	// header
	out = "// SGS/CPP-BC\n// warning: do not modify this file, it may be regenerated during any build\n";
	if( PRINT_TIMESTAMP )
	{
		out $= string_format( "// generated: {1}\n", os_date_string( "%c" ) );
	}
	out $= "\n";
	
	// includes
	foreach( include : includes )
	{
		out $= "#include \"" $ include $ "\"\n";
	}
	
	////////////////////////
	foreach( obj : objects )
	{
		if( obj.no_export )
			continue;
		
		ifuncs = clone( DEFAULT_OBJ_IFUNCS );
		
		// context changing
		dtorchanger = if( obj.is_lite, "", "\n\tC = callerCtx;" );
		tmpchanger = if( obj.is_lite, "", "\n\t_sgsTmpChanger<sgs_Context*> _tmpchg( C, callerCtx );" );
		mtdchanger = if( obj.is_lite, "", "\n\t_sgsTmpChanger<sgs_Context*> _tmpchg( data->C, callerCtx );" );
		
		// load the ifuncs
		foreach( item : obj.items )
		{
			if( item.what != "IFUNC" )
				continue;
			
			ifuncs[ item.type ] = item.name;
		}
		
		if( obj.no_destruct )
			ifuncs[ "destruct" ] = null;
		
		// method wrappers
		foreach( item : obj.items )
		{
			if( item.what != "METHOD" )
				continue;
			
			line = "";
			param_id = 0;
			foreach( i, param : item.params )
			{
				if( line != "" )
					line $= ", ";
				
				getvar3rd = "";
				ptype = param;
				if( param.last == "*"c )
				{
					getvar3rd = "Obj";
					ptype = string_part( param, 0, -1 );
				}
				fixedtype = cppbc_fix_type( ptype );
				
				if( fixedtype == "sgs_Context" || fixedtype == "SGS_CTX" )
					line $= "callerCtx";
				else
					line $= string_format( "sgs_GetVar{3}<{1}>()(callerCtx,{2})", fixedtype, param_id++, getvar3rd );
			}
			
			call_fmt = if( item.is_static, "{3}::{1}( {2} )", "data->{1}( {2} )" );
			line = string_format( call_fmt, item.name, line, obj.name );
			
			if( item.type == "SGS_MULTRET" )
				line = "return " $ line $ ";";
			else if( item.type != "void" )
				line = "sgs_PushVar(callerCtx," $ line $ "); return 1;";
			else
				line $= "; return 0;";
			
			if( item.is_static )
			{
				out $= string_translate(
"
static int _sgs_method__%OBJ%__%NAME%( sgs_Context* callerCtx )
{
	SGSFN( \"%OBJ%.%NAME%\" );
	%LINE%
}
"
				, { "%OBJ%" = obj.name, "%NAME%" = item.varname, "%LINE%" = line } );
			}
			else
			{
				out $= string_translate(
"
static int _sgs_method__%OBJ%__%NAME%( sgs_Context* callerCtx )
{
	%OBJ%* data; if( !SGS_PARSE_METHOD( callerCtx, %OBJ%::_sgs_interface, data, %OBJ%, %NAME% ) ) return 0;"
	$ mtdchanger $ "
	%LINE%
}
"
				, { "%OBJ%" = obj.name, "%NAME%" = item.varname, "%LINE%" = line } );
			}
		}
		
		// interface functions
		// - INHERITANCE & LOCAL STATIC INTERFACES
		foreach( ifname ,: OBJ_IFUNC_POS )
		{
			ifunc = @ifuncs[ ifname ];
			if( ifname == "convert" || ifname == "dump" || ifname == "getnext" )
			{
				args = "( sgs_Context* callerCtx, sgs_VarObj* obj, int param )";
				params = "( callerCtx, obj, param )";
			}
			else
			{
				args = "( sgs_Context* callerCtx, sgs_VarObj* obj )";
				params = "( callerCtx, obj )";
			}
			
			// inheritable function
			out ..= "\nint " .. obj.name .. "::_sgs_" .. ifname
				.. args .. "{ return ";
			if( ifunc === null )
				out ..= "SGS_ENOTSUP";
			else
				out ..= ifunc .. params;
			out ..= "; }";
			
			// local static interface
			out ..= "\nint " .. obj.name .. "::_sgslocal_" .. ifname
				.. args .. "{ return ";
			if( ifunc === null )
				out ..= "SGS_ENOTSUP";
			else
				out ..= "static_cast<" .. obj.name .. "*>( static_cast<sgsLiteObjectBase*>( obj->data ) )->" .. ifunc .. params;
			out ..= "; }";
		}
		out ..= "\n";
		
		// - OP_DESTRUCT
		out $= string_translate(
"
int %OBJ%::_sgsimpl_destruct( sgs_Context* callerCtx, sgs_VarObj* obj )
{"
	$ dtorchanger $ "
	this->~%OBJ%();
	return SGS_SUCCESS;
}
"
		, { "%OBJ%" = obj.name } );
		
		// - OP_GCMARK
		out $= "\nint " $ obj.name $ "::_sgsimpl_gcmark( sgs_Context* callerCtx, sgs_VarObj* obj )\n{";
		out $= tmpchanger;
		foreach( item : obj.items )
		{
			if( item.what != "GCREF" )
				continue;
			out $= "\n\tsgs_GCMarkVar( callerCtx, "$item.name$" );";
		}
		if( obj.inherited )
			out ..= "\n\t" .. obj.inherited[0] .. "::_sgs_gcmark( callerCtx, obj );";
		out $= "\n\treturn SGS_SUCCESS;\n}\n";
		
		// - OP_GETINDEX
		out $=
"
int " $ obj.name $ "::_sgsimpl_getindex( sgs_Context* callerCtx, sgs_VarObj* obj )
{"
	$ tmpchanger $ "
	char* str;
	if( sgs_ParseString( callerCtx, 0, &str, NULL ) )
	{";
		foreach( item : obj.items )
		{
			itemline = null;
			if( item.what == "METHOD" && obj.new_iface == false )
				itemline = "sgs_PushCFunc( callerCtx, _sgs_method__%OBJ%__"$item.varname$" );";
			else if( item.what == "PROPERTY" )
			{
				if( item.access <= 0 )
					itemline = "%TYPEFUNC%( callerCtx, "$item.name$" );";
			}
			else if( item.what == "PROPERTY_FUNC" )
			{
				itemline = "";
				if( item.validate !== null )
					itemline $= "if( !( "$item.validate$" ) ){ return SGS_EINPROC; } ";
				if( item.read === true )
					itemline $= "%TYPEFUNC%( callerCtx, "$item.srcname$" );";
				else if( item.read )
					itemline $= "%TYPEFUNC%( callerCtx, "$item.read$"() );";
				if( item.readcb )
					itemline $= "\n\t\t\t"$item.readcb$"();";
			}
			
			if( itemline )
			{
				out $= string_translate( "
		if( !strcmp( str, \"%NAME%\" ) ){ "$itemline$" return SGS_SUCCESS; }"
				, {
					"%OBJ%" = obj.name,
					"%NAME%" = if( isset( item, "varname" ), item.varname || item.name, item.name ),
					"%TYPEFUNC%" = if( cppbc_type_is_voidptr( item.type ), "sgs_PushPtr", "sgs_PushVar" ),
				} );
			}
		}
		foreach( item : obj.bstores )
		{
			if( !item.read )
				continue;
			out $= "
		if( sgs_PushIndex( callerCtx, " $ item.code
			$ ", sgs_StackItem( callerCtx, 0 ), sgs_ObjectArg( callerCtx ) ) ) return SGS_SUCCESS;";
		}
		out $= "
	}
	return " .. if( obj.inherited, obj.inherited[0] .. "::_sgs_getindex( callerCtx, obj )", "SGS_ENOTFND" ) .. ";
}
";
		
		// - OP_SETINDEX
		out $=
"
int " $ obj.name $ "::_sgsimpl_setindex( sgs_Context* callerCtx, sgs_VarObj* obj )
{"
	$ tmpchanger $ "
	char* str;
	if( sgs_ParseString( callerCtx, 0, &str, NULL ) )
	{";
		foreach( item : obj.items )
		{
			itemline = null;
			if( item.what == "PROPERTY" )
			{
				if( item.access >= 0 )
					itemline = ""$item.name$" = %TYPEFUNC%( callerCtx, 1%ISOBJ% );";
			}
			else if( item.what == "PROPERTY_FUNC" )
			{
				itemline = "";
				if( item.validate !== null )
					itemline $= "if( !( "$item.validate$" ) ){ return SGS_EINPROC; } ";
				if( item.write === true )
					itemline $= ""$item.srcname$" = %TYPEFUNC%( callerCtx, 1%ISOBJ% );";
				else if( item.write )
					itemline $= ""$item.write$"( %TYPEFUNC%( callerCtx, 1%ISOBJ% ) );";
				if( item.writecb )
					itemline $= "\n\t\t\t"$item.writecb$"();";
			}
			if( itemline )
			{
				out $= string_translate( "
		if( !strcmp( str, \"%NAME%\" ) ){ "$itemline$" return SGS_SUCCESS; }"
				, {
					"%OBJ%" = obj.name,
					"%NAME%" = if( isset( item, "varname" ), item.varname || item.name, item.name ),
					"%TYPEFUNC%" = if( cppbc_type_is_voidptr( item.type ), "sgs_GetPtr", "sgs_GetVar<" $ cppbc_fix_type( item.type ) $ ">()" ),
					"%ISOBJ%" = if( string_part( item.type, 0, 4 ) != "void" && item.type.last == "*"c, ", true", "" ),
				});
			}
		}
		foreach( item : obj.bstores )
		{
			if( !item.write )
				continue;
			out $= "
		if( sgs_SetIndex( callerCtx, " .. item.code
			$ ", sgs_StackItem( callerCtx, 0 ), sgs_StackItem( callerCtx, 1 ), sgs_ObjectArg( callerCtx ) ) ) return SGS_SUCCESS;";
		}
		out $= "
	}
	return " .. if( obj.inherited, obj.inherited[0] .. "::_sgs_setindex( callerCtx, obj )", "SGS_ENOTFND" ) .. ";
}
";
		
		// - OP_DUMP
		out $= "\nint " $ obj.name $ "::_sgsimpl_dump( sgs_Context* callerCtx, sgs_VarObj* obj, int depth )\n{";
		out $= tmpchanger;
		out $= "\n\tchar bfr[ "$ obj.name.length + 32 $" ];";
		out $= "\n\tsprintf( bfr, \""$obj.name$" (obj=%p, base=%p) %s\",";
		out ..= "\n\t\tstatic_cast< " .. obj.name .. "* >( static_cast<sgsLiteObjectBase*>( obj->data ) ),";
		out ..= "\n\t\tobj->data, depth > 0 ? \"\\n{\" : \" ...\" );";
		out $= "\n\tsgs_PushString( callerCtx, bfr );";
		out $= "\n\tif( depth > 0 )\n\t{";
		// 1. compile a list
		dumpvars = {};
		dumpvalid = {};
		dumpvarorder = [];
		foreach( item : obj.items )
		{
			if( item.what != "DUMP" && item.what != "PROPERTY" && item.what != "PROPERTY_FUNC" )
				continue;
			if( item.what == "PROPERTY" && item.access > 0 ) continue;
			if( item.what == "PROPERTY_FUNC" && !item.read ) continue;
			
			if( item.what == "PROPERTY_FUNC" && item.read !== true )
			{
				dumpvars[ item.name ] = item.read $ "()";
				dumpvalid[ item.name ] = item.validate;
			}
			else
			{
				dumpvars[ item.name ] = @item.srcname ?? item.name;
				dumpvalid[ item.name ] = @item.validate;
			}
			dumpvarorder.push( item.name );
		}
		foreach( item : obj.items )
		{
			if( item.what != "NODUMP" )
				continue;
			unset( dumpvars, item.name );
			dumpvarorder.remove( item.name );
		}
		// 2. push it
		dpcount = dict_size( dumpvars ) * 2;
		if( obj.inherited )
		{
			dpcount += 2;
			out ..= "\n\t\tsgs_PushStringLit( callerCtx, \"\\n[inherited] \" );";
			out ..= "\n\t\t" .. obj.inherited[0] .. "::_sgs_dump( callerCtx, obj, depth );";
		}
		foreach( name : dumpvarorder )
		{
			src = dumpvars[ name ];
			vld = dumpvalid[ name ];
			out $= "\n\t\t{ ";
			out $= "sgs_PushStringLit( callerCtx, \"\\n"$name$" = \" ); ";
			if( vld !== null )
			{
				out $= "if( !( "$vld$" ) ) sgs_PushStringLit( callerCtx, \"<inaccessible>\" ); else ";
			}
			out $= "sgs_DumpData( callerCtx, "$src$", depth ).push( callerCtx );";
			out $= " }";
		}
		out $= "\n\t\tsgs_StringConcat( callerCtx, " .. dpcount .. " );\n\t\tsgs_PadString( callerCtx );";
		out $= "\n\t\tsgs_PushStringLit( callerCtx, \"\\n}\" );";
		out $= "\n\t\tsgs_StringConcat( callerCtx, 3 );";
		out $= "\n\t}";
		out $= "\n\treturn SGS_SUCCESS;\n}\n";
		
		// new interface
		ifn = "NULL";
		if( obj.new_iface )
		{
			ifn = obj.name $ "__sgs_ifn";
			out $=
"
static sgs_RegFuncConst " $ obj.name $ "__sgs_funcs[] =
{";
			foreach( item : obj.items )
			{
				if( item.what != "METHOD" )
					continue;
				
				out $= "\n\t{ \"" $ item.varname $ "\", "
					$ "_sgs_method__" $ obj.name $ "__" $ item.varname $ " },";
			}
			out $=
"
	{ NULL, NULL },
};

static int " $ obj.name $ "__sgs_ifn( SGS_CTX )
{
	sgs_CreateDict( C, NULL, 0 );
	sgs_StoreFuncConsts( C, sgs_StackItem( C, -1 ),
		" $ obj.name $ "__sgs_funcs,
		-1, \"" $ obj.name $ ".\" );
	return 1;
}
";
		}
		
		// interface
		funcnames = [].resize( OBJ_IFUNC_COUNT );
		foreach( ifname, ifunc : ifuncs )
			funcnames[ OBJ_IFUNC_POS[ ifname ] ] = "_sgslocal_" .. ifname;
		out $=
"
static sgs_ObjInterface " $ obj.name $ "__sgs_interface =
{
	\"" $ obj.name $ "\",
	";
		
		foreach( ifunc : funcnames )
			out $= if( ifunc === null, "NULL, ", string_format( "{1}::{2}, ", obj.name, ifunc ) );
		
		inhptr = "";
		if( obj.inherited )
			inhptr $= ", &" $ obj.inherited[0] $ "::_sgs_interface";
		
		out $=
"
};
_sgsInterface " $ obj.name $ "::_sgs_interface(" $ obj.name $ "__sgs_interface, " $ ifn $ inhptr $ ");

";
	}
	
	return out;
}


function cppbc_parse_file( code, inclcode, includes )
{
	println( "In included code:" );
	inclcode = cppbc_preprocess( inclcode );
	inclobjects = cppbc_find_objects( inclcode );
	
	println( "In processed code:" );
	code = cppbc_preprocess( code );
	objects = cppbc_find_objects( code );
	
	foreach( io : inclobjects )
	{
		io.no_export = true;
	}
	objects = get_concat( inclobjects, objects );
	
	gcode = cppbc_generate_code( objects, includes );
	return gcode;
}


// ignore argument 0
argc--;
argv.shift();


global IN = null;
global OUT = null;
global INCLCODE = "";
global INCRENAME = null;
global PRINT_TIMESTAMP = false;
global NEW_IFACE = true;

function readfile( f )
{
	outf = @io_file_read( f );
	if( outf === null )
		ERROR( "failed to read '" $ f $ "'" );
	return outf;
}

for( i = 0; i < argv.size; ++i )
{
	if( argv[ i ] == "-add-timestamp" ){ PRINT_TIMESTAMP = false; }
	else if( argv[ i ] == "-def-old-iface" ){ NEW_IFACE = false; }
	else if( argv[ i ] == "-o" )
	{
		i++;
		if( i == argv.size )
			cppbc_error( "expected output name after -o" );
		OUT = argv[ i ];
	}
	else if( argv[ i ] == "-i" )
	{
		i++;
		if( i == argv.size )
			cppbc_error( "expected include file name after -i" );
		nic = readfile( argv[ i ] );
		if( nic === null )
			cppbc_error( "failed to read include file" );
		INCLCODE $= nic;
	}
	else if( argv[ i ] == "-iname" )
	{
		i++;
		if( i == argv.size )
			cppbc_error( "expected include file name override after -iname" );
		INCRENAME = argv[ i ];
	}
	else if( argv[ i ] == "-cmake0" )
	{
		i += 2;
		if( i >= argv.size )
			cppbc_error( "expected <output_name> <input_name> after -cmake0" );
		out = argv[ i - 1 ];
		in = argv[ i ];
		io_file_write( out, "#include \"" $ in $ "\"\n" );
		return;
	}
	else // input file
	{
		if( IN !== null )
			cppbc_error( "expected only one input file" );
		IN = argv[ i ];
	}
}

if( IN === null )
	cppbc_error( "expected at least 1 argument: source file" );

IN = string_replace( IN, "\\", "/" );
if( OUT === null )
{
	pathspl = string_explode( IN, "/" );
	ofsp = string_explode( pathspl.pop(), "." );
	ofsp.pop();
	pathspl.push( "cppbc_" $ string_implode( ofsp, "." ) $ ".cpp" );
	OUT = string_implode( pathspl, "/" );
}

code = cppbc_parse_file( readfile( IN ), INCLCODE, [INCRENAME ?? IN] );
io_file_write( OUT, code );


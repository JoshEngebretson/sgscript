
global ERRORS;
global tests_failed, tests_ran;

//
function section( x ){ print( "\n\t<< ", x, " >>\n" ); }
print( "\n\n-- CORE FUNCTIONS --\n" );
include_library( "type" );
//

var asz = [ 10, "huh", {}, 0.25, "end!" ];
var arr = [ 9, 3, 6, 2, 8 ];
function customSort( a, b ){ return a - b + ( a % 2 - b % 2 ) * 5000; } // even before odd
var mapping = [ 1, 3, 2, 5, 4 ];
var arr2 = [ 1, 5, 7, 3, 9 ];

	section( "array basic ops" );
testEqual( clone(arr).insert(0,1,4)$'', '[1,4,9,3,6,2,8]', "insert at beginning" );
testEqual( clone(arr).insert(-1,1,4)$'', '[9,3,6,2,8,1,4]', "insert at end" );
testEqual( clone(arr).insert(3,1,4)$'', '[9,3,6,1,4,2,8]', "insert at middle" );
ERRORS = ""; testEqual( clone(arr).insert(6,5), null, "insert - out of bounds" );
testEqual( ERRORS, "[W:array.insert(): index out of bounds]" );
testEqual( clone(arr).erase(0)$'', '[3,6,2,8]', "erase one at beginning" );
testEqual( clone(arr).erase(2)$'', '[9,3,2,8]', "erase one at middle" );
testEqual( clone(arr).erase(-1)$'', '[9,3,6,2]', "erase one at end" );
testEqual( clone(arr).erase(0,2)$'', '[2,8]', "erase many at beginning" );
testEqual( clone(arr).erase(1,3)$'', '[9,8]', "erase many at middle" );
testEqual( clone(arr).erase(-3,-1)$'', '[9,3]', "erase many at end" );
ERRORS = ""; testEqual( clone(arr).erase(4,7), null, "erase - out of bounds" );
testEqual( ERRORS, "[W:array.erase(): index out of bounds]" );
ERRORS = ""; testEqual( clone(arr).erase(4,-2), null, "erase - invalid range" );
testEqual( ERRORS, "[W:array.erase(): after resolving, index #1 must be smaller or equal than index #2]" );

	section( "array sizing" );
testEqual( clone(asz).resize(2)$'', '[10,huh]', "resize - 5 to 2" );
testEqual( clone(asz).resize(2).resize(5)$'', '[10,huh,null,null,null]', "resize - 5 to 2 to 5" );
testEqual( clone(asz).reserve(16).capacity, 16, "reserve - capacity check" );
testEqual( clone(asz).reserve(1).capacity, 5, "reserve - less than current" );

	section( "array.sort" );
testEqual( clone(arr).sort()$'', '[2,3,6,8,9]', "sort - basic integer sorting" );
testEqual( clone(arr).sort(true)$'', '[9,8,6,3,2]', "sort - reversed integer sorting" );

testEqual( clone(arr).sort_custom( customSort )$'', '[2,6,8,3,9]', "sort_custom" );
testEqual( clone(arr).sort_custom( customSort, true )$'', '[9,3,8,6,2]', "sort_custom (reversed)" );

testEqual( clone(arr).sort_mapped( mapping )$'', '[9,6,3,8,2]', "sort_mapped" );
testEqual( clone(arr).sort_mapped( mapping, true )$'', '[2,8,3,6,9]', "sort_mapped (reversed)" );

	section( "array utils" );
testEqual( clone(arr2).reverse()$'', '[9,3,7,5,1]', "reverse - odd" );
testEqual( clone(arr2).resize(4).reverse()$'', '[3,7,5,1]', "reverse - even" );
testEqual( [].reverse()$'', '[]', "reverse - none" );

testEqual( arr.find( "3" ), 1, "find - basic" );
testEqual( arr.find( "3", true ), null, "find - strict, empty" );
testEqual( arr.find( 3, true, 1 ), 1, "find - strict, matching offset" );
testEqual( arr.find( 3, true, 2 ), null, "find - strict, offset, empty" );


	section( "include_file" );
testEqual( include_file( "tests/s_fnexp.txt" ), true, "include_file - basic inclusion" );
global sqr;
testEqual( typeof( sqr ), "func", "include_file - received the variable?" );
sqr = null;
include_file( "tests/s_fnexp.txt" );
testEqual( typeof( sqr ), "null", "include_file - didn't rewrite the variable?" );
include_file( "tests/s_fnexp.txt", true );
testEqual( typeof( sqr ), "func", "include_file - did force-rewrite the variable?" );


	section( "include_shared" );
testEqual( include_shared( "bin/sgstest.exe" ), true, "include_shared - sgscript_main call [1]" );
testEqual( imported_var, 1337, "include_shared - sgscript_main call [2]" );

	section( "import_cfunc" );
testEqual( typeof( import_cfunc( "bin/sgstest.exe", "sgscript_main" ) ), "cfunc", "import_cfunc - retrieval of entry point" );
ERRORS = ""; testEqual( import_cfunc( "bin/nosuchfile", "sgscript_main" ), null, "import_cfunc - invalid file name" ); testEqual( ERRORS, "[W:import_cfunc() - file not found]" );
ERRORS = ""; testEqual( import_cfunc( "bin/sgstest.exe", "nosuchfunc" ), null, "import_cfunc - invalid function name" ); testEqual( ERRORS, "[W:import_cfunc() - procedure not found]" );


//
print( "\n\nTesting finished!\nRan ", tests_ran, " tests of which ", tests_failed, " failed.\n" );
//

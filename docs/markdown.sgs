
include_library( "string" );


function htmlencode( text )
{
	text = string_replace( text, "&", "&amp;" );
	text = string_replace( text, "<", "&lt;" );
	text = string_replace( text, ">", "&gt;" );
	text = string_replace( text, "'", "&#39;" );
	text = string_replace( text, "\"", "&quot;" );
	return text;
}

function uladjust( prev, cur )
{
	if( prev == cur )
		return "</li><li>";
	else if( prev < cur )
		return string_repeat( "<ul><li>", cur - prev );
	else // prev > cur
		return string_repeat( "</li></ul>", prev - cur ) $ ["","<li>"][ cur > 0 ];
}

function array_find( a, what )
{
	for( i = 0; i < a.size; ++i )
	{
		if( a[ i ] == what )
			return i;
	}
	return null;
}

function markdown2html( text )
{
	mdinline = function( text )
	{
		apply_tag = function( hstack, tag )
		{
			out = "";
			if( array_find( hstack, tag ) !== null )
			{
				tmp = [];
				while( ( x = hstack.pop() ) != tag )
				{
					out $= "</"$x$">";
					tmp.push( x );
				}
				out $= "</"$tag$">";
				while( tmp.size )
				{
					x = tmp.pop();
					out $= "<"$x$">";
					hstack.push( x );
				}
			}
			else
			{
				hstack.push( tag );
				out $= "<"$tag$">";
			}
			return out;
		};
		
		out = "";
		hstack = [];
		for( i = 0; i < text.length; ++i )
		{
			chr = text[ i ];
			if( chr == "*" )
			{
				if( i + 1 < text.length && string_cut( text, i+1, i+1 ) == "*" ) // bold
				{
					out $= apply_tag( hstack, "b" );
					i++;
				}
				else
				{
					out $= apply_tag( hstack, "i" );
				}
			}
			else if( chr == "`" )
				out $= apply_tag( hstack, "code" );
			else
				out $= htmlencode( chr );
		}
		while( hstack.size )
		{
			x = hstack.pop();
			out $= "</" $ x $ ">";
		}
		return out;
	};
	
	text = string_replace( text, ['\r\n','\r'], '\n' );
	
	lines = string_explode( text, "\n" );
	for( i = 0; i < lines.size; ++i )
	{
		L = lines[ i ];
		
		if( string_part( L, 0, 3 ) == "---" )
		{
			lines[ i ] = "</p><hr /><p>";
		}
		else if( string_part( L, 0, 2 ) == "- " )
		{
			lines[ i ] = "</p><ul><li>" $ mdinline( string_trim( L, "- ", LEFT ) );
			i++;
			ulevel = 1;
			while( i < lines.size )
			{
				L = lines[ i ];
				tr = string_trim( L, "-", LEFT );
				tr2 = string_trim( tr, " ", LEFT );
				dashcnt = L.length - tr.length;
				spacecnt = tr.length - tr2.length;
				if( !dashcnt || !spacecnt )
				{
					i--;
					break;
				}
				lines[ i ] = uladjust( ulevel, dashcnt ) $ mdinline( string_trim( L, "- ", LEFT ) );
				ulevel = dashcnt;
				i++;
			}
			if( i >= lines.size ) i--;
			lines[ i ] $= uladjust( ulevel, 0 ) $ "<p>";
		}
		else if( string_part( L, 0, 2 ) == "! " )
		{
			lines[ i ] = "<em>" $ mdinline( string_trim( L, "! ", LEFT ) ) $ "</em>";
		}
		else if( string_part( L, 0, 4 ) == "    " )
		{
			lines[ i ] = "</p><pre>" $ string_part( L, 4 );
			i++;
			while( i < lines.size )
			{
				L = lines[ i ];
				if( string_part( L, 0, 4 ) != "    " )
				{
					i--;
					break;
				}
				lines[ i ] = string_part( L, 4 );
				i++;
			}
			if( i >= lines.size ) i--;
			lines[ i ] $= "</pre><p>";
		}
		else if( string_part( L, 0, 2 ) == "==" )
		{
			cnt = L.length - string_trim( L, "=", LEFT ).length;
			L = string_trim( L, "= \t\v\r\n" );
			lines[ i ] = "</p><h"$cnt$">" $ mdinline( L ) $ "</h"$cnt$"><p>";
		}
		else if( string_trim( L ) == "" )
		{
			lines[ i ] = "</p><p>";
		}
		else
			lines[ i ] = mdinline( L );
	}
	
	return "<p>" $ string_implode( lines, "\n" ) $ "</p>";
}
